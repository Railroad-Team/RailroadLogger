plugins {
    id 'java'
    id 'maven-publish'
}

group = 'dev.railroadide'
version = '1.0.0'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release.set(21)
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-compress:1.27.1'
    implementation 'org.fusesource.jansi:jansi:2.4.2'

    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    compileOnly 'org.projectlombok:lombok:1.18.38'
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    repositories {
        mavenLocal()
        maven {
            url "https://maven.railroadide.dev/releases"
            credentials {
                username = System.getenv("RAILROAD_MAVEN_USERNAME")
                password = System.getenv("RAILROAD_MAVEN_PASSWORD")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact tasks.sourcesJar
        }
    }
}